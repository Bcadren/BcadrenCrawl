###############################################################################
# depths_encompass.des: Full level (encompassing) vaults for the Depths go here.
###############################################################################

# Setups for the below.
{{
    function grunt_profane_halls_setup(e)
        if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
            e.kmons('1 = base draconian / nothing w:25')
            e.kmons('2 = nonbase draconian / base draconian w:20')
            e.kmons('3 = draconian zealot / draconian monk w:2')
        else
            e.kmons('1 = deep elf fighter / deep elf conjurer / '
                    .. 'deep elf summoner / deep elf knight w:5 / '
                    .. 'deep elf mage w:5 / '
                    .. 'deep elf demonologist w:5 /'
                    .. 'nothing w:68')
            e.kmons('2 = deep elf knight / deep elf fighter w:20')
            e.kmons('3 = deep elf high priest')
        end
        e.subst('% = * %:20 .')
        e.lua_marker(':', fog_machine { cloud_type = "flame",
                                        pow_min = 10, pow_max = 10, delay = 10,
                                        size = 1, spread_rate = 0,
                                        walk_dist = 0, excl_rad = 0 })
    end

    function radiant_caverns_setup(e)
        e.set_random_mon_list(
            "glowing shapeshifter w:100 / air elemental / fire elemental / " ..
            "earth elemental / crystal guardian / molten gargoyle / " ..
            "war gargoyle / dancing weapon / very ugly thing band")
        e.mons("raiju band / spriggan air mage band / titan w:5 / " ..
               "storm dragon w:5 / wind drake w:5")
        e.mons("fire giant / fire dragon / molten gargoyle / " ..
               "salamander mystic / salamander firebrand w:2 / " ..
               "nothing w:42")
        e.mons("glowing shapeshifter / nothing")
        e.mons("very ugly thing w:5 / ugly thing")
        e.mons("ball lightning / nothing w:4")
        e.mons("insubstantial wisp / nothing")
        e.mons("crystal guardian")
        e.shuffle("wKLMN / lghij")
        e.shuffle("KLMN")
        e.shuffle("ghij")
        e.shuffle("JH")
        e.shuffle("34")
        e.shuffle("m6")
        e.subst("x = xxxxxbb")
        e.subst("s = .*$p|O")
        e.subst("u = ||$.O")
        e.kitem("p = w:50 potion of mutation / " ..
                "w:15 amulet of resist mutation ident:all / " ..
                "w:1 potion of cure mutation")
        e.kitem("O = | mimic")
        e.kitem("Q = potion of cure mutation")
        e.kitem("Z = amulet of resist mutation ident:all")
        e.kitem("U = potion of cure mutation / nothing")
        e.nsubst("u = 2:Q / 1:Z / 1:U / *:u")
        e.subst("x = x b:2")
        e.nsubst("H = 2:D / 2:E")
        e.nsubst("Y = 1:G / *:I")
        e.nsubst("J = 2:E / 2:4")
        e.nsubst(": = 3:a / *:.")
        e.nsubst("w = 4:r / *:w")
        e.nsubst("1 = 2:1 / *:.")
        e.kfeat("r = deep_water")
        e.kmons("r = water elemental")
        e.kmons("n = glowing shapeshifter / w:5 dancing weapon / w:25 nothing")
        e.kmons("m = very ugly thing w:5 / ugly thing / w:25 nothing")
        e.kmons("jL = war gargoyle / crystal guardian / w:1 iron golem / " ..
                "w:21 nothing")
        e.kmons("iN = earth elemental / nothing")
        e.kmons("h = w:30 air elemental / glowing shapeshifter / " ..
                " w:30 nothing")
        e.kmons("g = w:30 water elemental / glowing shapeshifter / " ..
                "w:30 nothing")
        e.kmons("q = war gargoyle / crystal guardian / " ..
                "glowing shapeshifter / earth elemental")
        e.kmons("tV = ball lightning / fire vortex / fire elemental / " ..
                "air elemental / wind drake / nothing w:75")
        e.kmons("S = gargoyle / molten gargoyle / w:1 war gargoyle / " ..
                "w:21 nothing")
        e.kmons("D = dancing weapon")
        e.kmons("E = glowing shapeshifter")
        e.kmons("K = molten gargoyle / fire elemental / " ..
                "w:50 earth elemental / w:70 nothing")
        e.kmons("M = earth elemental / fire elemental / w:20 nothing")
        e.kmons("I = silver statue / ice statue / w:1 orange crystal statue")
        e.kmons("y = glowing shapeshifter / war gargoyle w:5 / " ..
                "crystal guardian w:5")
        e.kmons("z = crystal guardian / w:30 dancing weapon")
        e.kfeat("A = stone_arch")
        e.marker("A = lua:props_marker { portal=1 }")
    end
}}

default-depth: Depths:2-, !Depths:$

###############################################################################
# The River Lethe
#
# The two orange statues are placed for maximum pain -- one you're likely to
# first see while flying, the other is hard to target with disintegration
# through its protective plants.
#
# Lemuel
#
NAME:    lemuel_river_lethe
TAGS:    no_rotate no_monster_gen no_item_gen uniq_d_encompass
ORIENT:  encompass
MONS:    plant / fungus w:2
MONS:    wolf / warg / hell hound / raiju w:4
MONS:    shadow w:20 / wraith w:20 / lost soul w:2 / \
         shadow wraith / phantasmal warrior / skeletal warrior
MONS:    dancing weapon / war gargoyle w:2
MONS:    spectral spriggan / spectral centaur / spectral naga / \
         spectral merfolk / spectral ogre / spectral troll / \
         spectral minotaur w:1 / spriggan simulacrum w:2 / centaur simulacrum w:1 / \
         naga simulacrum w:2 / troll simulacrum w:2 / minotaur simulacrum w:1 / \
         merfolk simulacrum w:2 / vampire / wraith / wight
MONS:    mana viper / eidolon w:5 / soul eater w:5 / \
         death drake w:5 / flayed ghost / shadow dragon w:2
MONS:    ancient lich w:20 / shadow fiend
KMONS:   8 = Maud, patrolling giant orange brain
KMONS:   9 = patrolling giant orange brain
KMONS:   0 = orange crystal statue
KMONS:   B = giant eyeball
KMONS:   C = 0 band
KMONS:   W = patrolling water elemental w:40 / \
             patrolling hydra simulacrum / patrolling hydra spectre / \
             patrolling anaconda simulacrum / patrolling anaconda spectre / \
             patrolling snapping turtle zombie
ITEM:    any scroll
KITEM:   e = any / any good_item w:4
KITEM:   8B = potion of experience ident:type q:1 / any good_item
KITEM:   9 = |
KITEM:   0 = potion of restore abilities ident:type
KFEAT:   8e = >
KFEAT:   W = w
NSUBST:  h = 1:+ / *:x, H = 1:+ / *:x
SHUFFLE: dD/eE/fF/gG
SUBST:   D = +, EFG = x
NSUBST:  e = 1:e / 1:* / *:%, f = 1:e / *:%, g = 1:e / *:%
SUBST:   | = |*
SHUFFLE: 0A
SUBST:   A = xw, 1 = 11.
# (previously) 762 ;, 372 ., SUBST: . = .:101 5:4, ; = .:201 1:3
NSUBST:  . = 12:5 / 2:. / 4 = 5. / *:., ; = 9:3 / 2:. / 4 = 3. / *:.
NSUBST:  . = 13:C / 4 = C. / *:., w = 8:W / 4 = W. / *:w
MARKER:  O = lua:props_marker { portal=1 }
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccccxxxxxx
xx89.h$xxxwwwxxxxcccccccccccccccccccccccccccxxxxxxxxcc..1......cxxxxxx
xw99.h$$Hwwwwxxxxc%.b..+...+..........%c***c;;xxxxx;cc.....1...cxxxxxx
xw..Oh$$Hxwwwxxxxc..7..ccccc...........c***c;;;xxx;;cc.1.1...1.cxxxxxx
xww..h$$Hxwwwwxxxc.....cxxxc...........c***c;;;;xx;;;+.....1..1cxxxxxx
xww..xxxHwwww;xxxc%.}.%cxxxc...........c...c;;;;xx;;cc..1.111..cxxxxxx
xwww.xxxwww;;;;xxccccccccxxcc+ccc+ccccccc+cc;;;;xx;;cc.111110..cxxxxxx
xwwwxxxwwww;;;;;xxxxxxxxxxxc...c...c.......c;;;;x;;;cc..111A..1cxxxxxx
xwwwxxxwwwww;;6;;xxxxxxxxxxc...c...c.w.....c;;;;;;;;cc.111111||cxxxxxx
xwwwxxxwwwww;;;;;;;xxxxxxxxc...+...c.w.....+;;;;;;;;cc..1116.|Bcxxxxxx
xwwwxxxwwww;;6;;;;;;;;xxx;;c...c...c.w.....c;;;;;;;;ccccccccccccxxxxxx
xwwwxxxwww;;;;;;;;;;;;;;;;;cc+ccc+cc..w....c;;;;;;;;;;xxxxxxxxxxxxxxxx
xwwwxxxwww;;;;;;;;;;;;;;;;;+.......+..w....cccc;;;;;;xxxxxxxxxxxxxxxxx
xwwwwxx;ww;;;;;;;;;;;;;;;;;cccc+cccc...w...c$$c;;;;;;;;xxxxxxxxxxxxxxx
xwwwwxx;w;;6;;;;cccc;;;;;;;c.......c...w...c$$c;;;;;;;;;;;xxxxxxxxxxwx
xwwwwxx;;;;;;;;cccccc;;;;;;c.......c+ccwwccc+cc;;;;;;;;;;;;;xxxxxxxxwx
xwwwwxx;;;6;;;cc*3.+;;;;;;;c.......c.c..ww....c;;;;;6;;;;;;;;xxxxxxwwx
xxwwwwx;;;;;;;;cccccc;;;;;;c5%5%5%5c.+...ww...c;;;;;;;;;;;;;;;xxxxxwwx
xxxwwwxx;;;;;;;;cccc;;;;;;;ccccccccc.c...ww...c;;;;;;;;;;;;;;;xxxxxwwx
xxxwwwxx;;;;;;;;;;;;;;;;;;;c$$+....+.c....www.c;;;;;;;ccccccccxxxxxwwx
xxxwwxx;;cccc;;;;;;;;;;;;;;c$$cccccc.c%....wwwc;;;;;;;c.....$$Dddxxwwx
xxxwwwx;cccccc;;;;;;xx;;;;;c$$cccccc+cccccccwww;;;;;;;c.....$$xdd+wwwx
xxxwwwxcc*3$+;;;;;;xxxx;;;;ccccccccc.....c...www;;ccccc.......xxxxxwwx
xxxwwwxxcccccc;;;;xxxxx;;;;;;;cccccc.....+...www;;c...c.......xeexxwwx
xxxwwwxx;cccc;;;;xxxxxx;;;;;;;cccccc.....c...www;cc.55c.......Eee+wwwx
xxxwwwxx;;;;;;;;xxx4..c;;;;;;;cccccc.....cccwww;;;+.55+...O...xxxxxwwx
xxxwwwxx;;;;;;;;;xx4O.c;;;;;;;;;;;;c%....c;wwww;;cc.55c.......Fffxxwwx
xxxwwwxx;;;;;;;;;;x4..+;;;;;;;;;;;;cccccccwwww;;;;c...c.......xff+wwwx
xxxwwwxx;cccc;;;;xxx..c;;;;;;;;;;;;;;;;;wwwwww;;;;ccccc.......xxxxxwwx
xxxwwwxxcccccc;;;;;xx%c;;;;;;;;;;;;;;;;wwwwwww;;;;;;;;c.....$$xggxxwwx
xxxwwwxcc*3$+;;;;;;xxxx;;;;;;;;;;;;;;;wwwwwwwww;;;;;;;c.....$$Ggg+wwwx
xxxwwwxxcccccc;;;;xxxwxx;;;;;;x;;;;;wwwwwwwwwww;;;;;;;ccccccccxxxxxwwx
xxxwwwxxxcccc;;x;;;xwwxxx;;;;;xx6wwwwwwwwwwwwww;;;;;;;;;;;;;;;xwwwwwwx
xxxwwwxxxxwww;;;;;;wwwwxw;;;;xxxwwwwwwwwwwwxxwww;;;;6;;;;;wwwwwwwwwwwx
xxxwwwxxxxxwwwwwwwxwwwwwwwwwxxxwwwwwwwwwwwwxxxwwwwww;;wwwwwwwwwwwwwwxx
xxxxwwwwxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxwwwwwwwwwwwwwwwwwwwwwxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxwwwwwwwxxxxxwwwwwwwwwwwwwwwwwwwxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxwwwwwwwwwwwwwwwwwwxxxxxxx
xxxxxwwwwwwwwwwwwwwwwAxwwwwwwwxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxx
xxxxxxwwwwwwwwwwwwwxxxxwwwwwwwwwwwwwxxx0wwwwwwwwwwxxwwwwwwwwxxxxxxxxxx
xxxxxxxwwwwwxxxxxxxxxxwwwwwwwxxwwwwxxxxxwwwwwwwwwxxxwwxwwwwxxxxxxxxxxx
xxxxxxxx;6;;xxx222xxxxxxwwwwxxwww2222xxxxx22wwwwwwxxxxxxxxxxxxxxxxxxxx
xxxxxxx>;;;xxx;;;;;xx2222wwwwww;;;;;;;;xxxx22;;;;wwwwxxxxxxxxxxxxxxxxx
xxxxxxxxx;xxxx;;;;;;;;;;;;ww;;;;;;;;;;;;;;;;;;;;;;;;wwwxxxxxxxxxxxxxxx
xxxxxxxxxxxx;;;;2;;;;;;;;;;;;;;;;;);;;;;;2;;;;;;;;;;wwwxxxxxxxxxxxxxxx
xxxxxxxxxxxx;;;;;;;;;;;;;;;;;;;;;[](;;;;;;;;;;;;;;;;2wxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx{xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# The New Box Level (dp)
#
NAME:    box_level_dp
TAGS:    no_pool_fixup no_rotate uniq_d_encompass
ORIENT:  encompass
: local brnd = crawl.random2(3)
: if brnd == 0 then
MONS:   orc priest / orc high priest / orc sorcerer / orc knight / \
        deep elf priest / deep elf conjurer / deep elf knight / nothing
MONS:   orc warlord / deep elf annihilator
: elseif brnd == 1 then
MONS:   spriggan rider w:12 / spriggan berserker w:12 / boggart / \
        ogre mage w:8 / ettin / stone giant / frost giant w:8 / nothing
MONS:   spriggan air mage / fire giant
: else
MONS:   salamander w:8 / salamander mystic w:12 / \
        tengu conjurer / tengu warrior / nothing w:5
MONS:   salamander firebrand / tengu reaver
: end
KMONS:   L = fire dragon
KMONS:   ~ = ice dragon
KMONS:   z = killer bee
KMONS:   Z = queen bee
ITEM:    ring of teleportation
ITEM:    scroll of teleportation
ITEM:    amulet of stasis
KITEM:   gzZ = royal jelly w:10 / nothing w:40
KFEAT:   L = l
KFEAT:   ~ = w
KFEAT:   ^ = known teleport trap / known Zot trap
KFEAT:   T = known permanent teleport trap
NSUBST:  T = 1:. / *:T
NSUBST:  w = 1:~ / *:w, l = 1:L / *:l
NSUBST:  A = 1:+ / *:b, B = 1:+ / *:b
NSUBST:  C = 1:+ / *:b, D = 1:+ / *:b
NSUBST:  E = 1:+ / *:x, N = 1:= / *:n
SUBST:   ' : ''l. , ' = l.
SUBST:   " : ""W. , " = Ww..
NSUBST:  * = 1:f / *:*
SUBST:   * = * % % $ $ e
SUBST:   ; = 9 0
SUBST:   ! = %$, & = *%
KITEM:   V = *
COLOUR:  a = yellow
TILE:    a = wall_wax
SUBST:   a = x
MARKER:  P = lua:props_marker { portal=1 }
KFEAT:   P = stone_arch
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.......xccccccxxxxxxx.........................................w"..xxx
x.(...{.xc....cbbbbbxx................xxx...................."w......x
x.......++.U..++.].++.....'''....'l'....xxx................."ww"..)..x
x...[...xc....cbbbbbxx...'''..''lllll'....xx...vvEvvEv......"ww"....xx
x.......xcccccccxxxxxx.......'llllllll.....x...vd...9v...."www".....xx
xxxxxxxxxxxxxxxxxxxxxx.......'lllllll'.....x...E.....E.."ww.w".....xxx
x.............................'lllll'.....xx...v9....v.www..w".....xxx
x....""w.............".xxx......'ll'......x....vvEEEEvwww.www.....xxxx
x.."wwwww"..........w....xxxx............xx....."wwwwwwww.ww"....xxxxx
x.wwwwwwww"........w........xxxxxxx....xxx........"www"...ww"...xxxxxx
x..wwwwwww"......"ww......""......xxxxxx....."...........ww"....xxxxxx
xx..""ww".......www....."wwwwwwwwww........."www"......wwwww"....xxxxx
xxx.........."wwww"..""wwwwwwwwwwwwwwwwww".www"...wwwwwwwwwwww.....xxx
xxxxx....""wwwwwwwwWWwwwwww.......wwwwwwwwwwwwwwwwwwwwwww.....xx....xx
xxx..""wwwwwwwwwwwwWWww"......G.G......"wwwwxxww".........xxxxx.....xx
x."wwwwwwwwwwwwwxxx__xxxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx....x
xwwwwwwww".."wwwxww.."w........................................>xx...x
xxwwww""....."xwxw"...ww........................................xx...x
xxww".........xxxw..............................................xx..xx
xww............xx"w...ccccccccccccccccccNcccccccccccccccccc.....xx..xx
xw"...xxx.......x..w..c^..*...*...*...*...*...*...*...*...c.....x...xx
xw....x!x..xxx..x..w..c...................................c.....x...xx
xxx...x+x..x!x..x.....c*..;...;...;...;...;...;...;...;..*c.....xx..xx
xxx....1..1+!x..x.....c...................................c.....xx...x
x..........xxx.xx.....c.......bbbbbbbbbbTbbbbbbbbbb.......c.....xx..xx
x..xxxx........xx.....c.......b.........b.........b.......c.....x...xx
x..x!!+1..xxxx..x.....c*..;...b.bbbAbbb.b.bbbBbbb.b...;..*c.....x..xxx
x..xxxx..1+!!x..x.....c.......b.b8...|b.b.b|...8b.b.......c.....xx.xxx
x.........xxxx..x.....c.......b.A|.P.|A.b.B|.P.|B.b.......c.....xx.xxx
x.......1.......x.....c.......b.b|...8b.b.b8...|b.b.......c.....x...xx
xxx....121....xxx.....c*..;...b.bbbAbbn.b.nbbBbbb.b...;..*c.....x...xx
x!x...cc+cc...xxx.....c.......b.........b.........b.......c.....xx..xx
x&+1..c!2!c...xxx.....N.......Tbbbbbbbbb.bbbbbbbbbT.......N.....x..xxx
x!x...c&&&c...xxx.....c.......b.........b.........b.......c.....x..xxx
xxx...ccccc...xxx.....c*..;...b.bbbDbbn.b.nbbCbbb.b...;..*c.....x.xxxx
x...............x.....c.......b.b|...8b.b.b|...8b.b.......c.....x.xxxx
xx...1...xxxx...x.....c.......b.D|.P.|D.b.C|.P.|C.b.......c.....x..x.x
xx..x+x.1+!!x...x.....c.......b.b8...|b.b.b8...|b.b.......c.....xx...x
x...x!x..xxxx...x.....c*..;...b.bbbDbbb.b.bbbCbbb.b...;..*c.....xxx.xx
x...xxx.........x.....c.......b.........b.........b.......c.....xxx.xx
x.........xxxxxxx.....c.......bbbbbbbbbbTbbbbbbbbbb.......c.....xxx..x
xxxx......+*9*9*+.....c...................................c.....xxxx.x
xxxx..xx..xxxxxxx.....c*..;...;...;...;...;...;...;...;..*c.....x....x
x.....xx........x.....c...................................c.....x.xx.x
x.xx.....xxx....x.....c...*...*...*...*...*...*...*...*..^c.....x.xx+x
x.xx....1+!x..xxx.....ccccccccccccccccccNcccccccccccccccccc.....xxx..x
x....xx..xxx..xxx...............................................xx..xx
x....xx.......xxx.........................................z..z..x....x
xx............xxx<.............................................zxa...x
xx..x......xx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxazzxx
xxx........xx.....xxx...xx.x..xx...xxaaaaaaagaaga.a..aaaaaaaaaaaaazzxx
xxxx..xx..............x...x...xx.xx.xaaazazzaaza.a.aa.za.z.aaaa.a.aazx
xxxxxxxxxxx....x..xx.x.xx.x..x..xx}..xagazaaZzazzaa..aa..aa....z.agaxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Profane Halls, by Grunt.
#
default-depth:

NAME: grunt_profane_halls
TAGS: no_monster_gen uniq_d_encompass
PLACE: Depths:$
DEPTH: Depths:2-
WEIGHT: 1 (Depths:$), 10
ORIENT: encompass
SHUFFLE: {[(<
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
SUBST: }])> = G
: else
SHUFFLE: }])>
: end
SUBVAULT: d : grunt_profane_temple
SUBVAULT: e : grunt_profane_temple
SUBVAULT: f : grunt_profane_temple
SUBVAULT: g : grunt_profane_temple
NSUBST: D = 1:K / 1:L / 1:M / 1:N
: grunt_profane_halls_setup(_G)
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
KMONS: K = storm dragon
KMONS: L = shadow dragon
KMONS: M = quicksilver dragon
KMONS: N = golden dragon
: else
KMONS: K = deep elf blademaster / deep elf master archer
KMONS: L = deep elf annihilator
KMONS: M = deep elf death mage
KMONS: N = deep elf sorcerer
: end
KMONS: 0 = 0 band w:15 / nothing
KMONS: 9 = 9 band / 0 band
KMASK: 1290KLMN = !no_monster_gen
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
SUBVAULT: H : grunt_zot_portal
: else
SUBVAULT: H : grunt_profane_loot
: end
MAP
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccdddddddddddddddcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccdddddddddddddddcccccccccccccccccccccccccccccccc
ccccccccccccccc.ccccccccccccccccdddddddddddddddcccccccccccccccc.ccccccccccccccc
cccccccccccccc..ccccccccccccccccdddddddddddddddcccccccccccccccc..cccccccccccccc
ccccccccccccc.G.cccGcGcGGcGcGcccdddddddddddddddcccGcGcGGcGcGccc.G.ccccccccccccc
ccccccccc.......c..............cdddddddddddddddc..............c.......ccccccccc
ccccccccc.{.....+......0.......+ddddddddddddddd+.......0......+.....[.ccccccccc
ccccccccc.......+......0.......+ddddddddddddddd+.......0......+.......ccccccccc
ccccccccc...U...c..............cdddddddddddddddc..............c...U...ccccccccc
cccccccc........cccGcGcGGcGcGcccdddddddddddddddcccGcGcGGcGcGccc........cccccccc
ccccccc.G.....}.ccccccccccccccccdddddddddddddddcccccccccccccccc.].....G.ccccccc
cccccc..........cccccccccccccccccdddddddddddddccccccccccccccccc..........cccccc
cccccccccc++ccccccccccccccccc...cdddddddddddddc...ccccccccccccccccc++cccccccccc
ccccccccc....ccccccccccccc......+ddddddddddddd+......ccccccccccccc....ccccccccc
ccccccccG....Gccccccccccc.......++ddddddddddd++.......cccccccccccG....Gcccccccc
ccccccccc....cccccccccc%...9.....+ddddddddddd+.....9...%cccccccccc....ccccccccc
ccccccccG....GccccccccU*.........ccdddddddddcc.........*UccccccccG....Gcccccccc
ccccccccc.00.cccccccc%*.......cccccdddddddddccccc.......*%cccccccc.00.ccccccccc
ccccccccG....Gcccccc........ccc%%.ccdddddddcc.%%ccc........ccccccG....Gcccccccc
ccccccccc....cccccc.......++c......ccdddddcc......c++.......cccccc....ccccccccc
ccccccccG....Gcccc...9...c+.........ccccccc.........+c...9...ccccG....Gcccccccc
ccccccccc....cccc.......cc........1....:....1........cc.......cccc....ccccccccc
cccccccccc++ccccc......cc..............1..............cc......ccccc++cccccccccc
cccceeeeeeeeeeecc++...cc.....G.......G...G.......G.....cc...++ccfffffffffffcccc
cccceeeeeeeeeeeeee++ccc%.........1.G...D...G.1.........%ccc++ffffffffffffffcccc
cccceeeeeeeeeeeeeeeeccc%.....1....2.........2....1.....%cccffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeecc.........G.:%HHHHH%:.G.........ccffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeecc..........%HHHHHHH%..........ccfffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec.......G..HHHHHHHHH..G.......cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec..........HHHHHHHHH..........cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec:.G..1..D.HHHHHHHHH.D..1..G.:cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec..........HHHHHHHHH..........cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec.......G..HHHHHHHHH..G.......cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeecc..........%HHHHHHH%..........ccfffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeecc.........G.:%HHHHH%:.G.........ccffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeccc%.....1....2.........2....1.....%cccffffffffffffffffcccc
cccceeeeeeeeeeeeee++ccc%.........1.G...D...G.1.........%ccc++ffffffffffffffcccc
cccceeeeeeeeeeecc++...cc.....G.......G...G.......G.....cc...++ccfffffffffffcccc
cccccccccc++ccccc......cc..............1..............cc......ccccc++cccccccccc
ccccccccc....cccc.......cc........1....:....1........cc.......cccc....ccccccccc
ccccccccG....Gcccc...9...c+.........ccccccc.........+c...9...ccccG....Gcccccccc
ccccccccc....cccccc.......++c......ccgggggcc......c++.......cccccc....ccccccccc
ccccccccG....Gcccccc........ccc%%.ccgggggggcc.%%ccc........ccccccG....Gcccccccc
ccccccccc.00.cccccccc%*.......cccccgggggggggccccc.......*%cccccccc.00.ccccccccc
ccccccccG....GccccccccU*.........ccgggggggggcc.........*UccccccccG....Gcccccccc
ccccccccc....cccccccccc%...9.....+ggggggggggg+.....9...%cccccccccc....ccccccccc
ccccccccG....Gccccccccccc.......++ggggggggggg++.......cccccccccccG....Gcccccccc
ccccccccc....ccccccccccccc......+ggggggggggggg+......ccccccccccccc....ccccccccc
cccccccccc++ccccccccccccccccc...cgggggggggggggc...ccccccccccccccccc++cccccccccc
cccccc..........cccccccccccccccccgggggggggggggccccccccccccccccc..........cccccc
ccccccc.G.....).ccccccccccccccccgggggggggggggggcccccccccccccccc.>.....G.ccccccc
cccccccc........cccGcGcGGcGcGcccgggggggggggggggccGcGccGGcGcGccc........cccccccc
ccccccccc...U...c..............cgggggggggggggggc..............c...U...ccccccccc
ccccccccc.......+......0.......+ggggggggggggggg+.......0......+.......ccccccccc
ccccccccc.(.....+......0.......+ggggggggggggggg+.......0......+.....<.ccccccccc
ccccccccc.......c..............cgggggggggggggggc..............c.......ccccccccc
ccccccccccccc.G.cccGcGcGGcGcGcccgggggggggggggggccGcGccGGcGcGccc.G.ccccccccccccc
cccccccccccccc..ccccccccccccccccgggggggggggggggcccccccccccccccc..cccccccccccccc
ccccccccccccccc.ccccccccccccccccgggggggggggggggcccccccccccccccc.ccccccccccccccc
ccccccccccccccccccccccccccccccccgggggggggggggggcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccgggggggggggggggcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

# Here just in case people want to see the original base layout.
NAME: grunt_profane_temple_base
TAGS: grunt_profane_temple allow_dup
WEIGHT: 0
: grunt_profane_halls_setup(_G)
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
%%...........%%
%G...........G%
...............
......1.1......
...............
.......U.......
...............
..1.........1..
...............
....1.....1....
.....G...G.....
 .............
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_geh
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 4 = 8:4 / *:., 5 = 1:6 / *:5
KMONS: 4 = red devil / hell hound / efreet w:5
KMONS: 5 = sun demon
KMONS: 6 = salamander firebrand / balrug
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
ll...l%%%l...ll
l....l%%%l....l
...............
..ll...5...ll..
..l4..lll..4l..
..l..lllll..l..
..l.5lllll5.l..
..l..lllll..l..
..l4..lll..4l..
..ll...5...ll..
...............
 ....l...l....
 ....l4.4l....
 ....l...l....
  ...l4.4l...
  ...l...l...
   ..l4.4l..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_coc
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 4 = 8:4 / *:., 5 = 1:6 / *:5
KMONS: 4 = blue devil / simulacrum w:20 / freezing wraith
KMONS: 5 = ice devil
KMONS: 6 = blizzard demon / ice dragon
KMONS: 8 = ice statue
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
wwww.......wwww
www....5....www
ww...........ww
w...4.www.4...w
.....wwwww.....
....ww%%%ww....
..4.ww%8%ww.4..
....ww%%%ww....
.....wwwww.....
....4.www.4....
...W.......W...
 ..WW.5.5.WW..
 ...W.....W...
 ...WW...WW...
  ..4W4.4W4..
  ...W...W...
   .........
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_tar
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 4 = 8:4 / *:., 5 = 1:6 / *:5
KMONS: 4 = shadow / wraith / flying skull
KMONS: 5 = soul eater
KMONS: 6 = reaper w:18 / lich w:5 / bone dragon w:4
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
.%%%.......%%%.
.ccc...4...ccc.
...............
.4....ccc....4.
.....c...c.....
...4..5V5..4...
.....c...c.....
.4....ccc....4.
...............
.ccc...4...ccc.
..5..G...G..5..
 .....4.4.....
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_dis
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 4 = 8:4 / *:., 5 = 1:6 / *:5
KMONS: 4 = iron devil
KMONS: 5 = skeletal warrior / iron troll w:1
KMONS: 6 = iron dragon / war gargoyle
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
COLOUR: G = cyan
TILE: G = dngn_statue_iron_golem
: set_feature_name("granite_statue", "iron statue")
KMASK: 12345 = !no_monster_gen
MAP
%%.5.......5.%%
%G...........G%
.....4...4.....
....4.v.v.4....
.....v...v.....
...4...G...4...
.....v...v.....
....4.v.v.4....
.....4...4.....
...............
.G...G...G...G.
 ..5.......5..
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_loot
TAGS: grunt_profane_loot no_monster_gen
: grunt_profane_halls_setup(_G)
SHUFFLE: }])>
SHUFFLE: KLMN
KMONS: K = storm dragon
KMONS: L = shadow dragon
KMONS: M = quicksilver dragon
KMONS: N = golden dragon
KITEM: KLMN> = acquire any
KITEM: 12 = any
MAP
  cc+cc
 cc1%1cc
cc2%K%2cc
c1%***%1c
+%L*>*N%+
c1%***%1c
cc2%M%2cc
 cc1%1cc
  cc+cc
ENDMAP

NAME: grunt_zot_portal
TAGS: grunt_zot_portal
COLOUR: c = lightmagenta
TILE: c = wall_zot_magenta
COLOUR: JO.1+ = magenta
FTILE: JO.1+ = floor_hall
KFEAT: O = enter_zot
KMONS: 1 = place:Zot:1
MARKER: J = lua:fog_machine { cloud_type = "magical condensation", \
                              pow_min = 3, pow_max = 5, \
                              delay_min = 5, delay_max = 15, \
                              size = 1, walk_dist = 4, \
                              spread_rate = 70 }
MAP
  cc+cc
 cc...cc
ccJ.1.Jcc
c.......c
+.1.O.1.+
c.......c
ccJ.1.Jcc
 cc...cc
  cc+cc
ENDMAP

default-depth: Depths:2-, !Depths:$

################################################################################
# A bizarre, imposing crystal construct, radiating great elemental magic
# that lures in and alters enemies often rare, strange, and nasty.
# Vaguely, vaguely inspired by Crawl Alternative's Jade Caves
# and the non-livings sets for the proposed Radiant Caverns.
# Stairs and greater threats are scattered throughout the level:
# anticipating dangers and skipping through the level may be quite difficult.
#
NAME:    hangedman_crystal_crosses_colossus
TAGS:    no_monster_gen no_item_gen no_pool_fixup uniq_d_encompass
ORIENT:  encompass
MONS:    deep elf conjurer / deep elf knight w:7 / wizard / ogre mage / \
         centaur warrior w:8 ; shortbow ego:flaming | shortbow ego:freezing . arrow / \
         two-headed ogre w:5 ; great mace ego:freezing ident:type . \
         great mace ego:flaming ident:type / efreet w:5 / \
         rakshasa w:5 ; wand of flame | wand of frost . whip . robe
MONS:    very ugly thing w:15 / shapeshifter / unseen horror w:15 / \
         chaos spawn / fire elemental / water elemental w:5 / \
         air elemental / earth elemental w:5 / mana viper w:5 / \
         insubstantial wisp / gargoyle w:5 / wolf spider simulacrum w:3 / \
         elephant simulacrum w:3 / deep troll simulacrum w:3 / \
         lindwurm perm_ench:shapeshifter w:7 / lindwurm w:3 / \
         dancing weapon w:5 ; trident ego:flaming | trident ego:freezing | \
         war axe ego:flaming | war axe w:15 ego:freezing |  \
         scimitar w:15 ego:freezing | scimitar ego:flaming | \
         dire flail w:5 ego:flaming | dire flail w:5 ego:freezing
MONS:    frost giant w:5 / fire giant w:5 / naga mage w:15 / hell knight w:5 / \
         salamander mystic w:5 / spriggan air mage / deep troll earth mage / \
         yaktaur captain ; crossbow ego:flaming | crossbow ego:freezing . bolt / \
         rakshasa w:5 ; robe . wand of fire | wand of cold . \
         glaive w:2 ego:flaming | glaive w:2 ego:freezing
MONS:    lorocyproca w:5 / fire dragon perm_ench:shapeshifter w:12 / fire dragon w:3 / \
         ice dragon perm_ench:shapeshifter w:12 / ice dragon w:3 / \
         war gargoyle w:5 / glowing shapeshifter / ghost moth w:1 / \
         boulder beetle / anaconda simulacrum w:4 / harpy simulacrum w:4 / \
         hydra simulacrum w:4 / fire dragon simulacrum w:3 / fire crab w:5 / \
         dancing weapon w:15 ; glaive ego:flaming | glaive ego:freezing | \
         battleaxe w:5 ego:flaming | battleaxe ego:freezing | \
         great sword ego:flaming | great sword w:5 ego:freezing | \
         great mace ego:flaming | great mace ego:freezing
KMONS:   A = fire giant ; battleaxe ego:freezing ident:type
KMONS:   B = frost giant ; great sword ego:flaming ident:type
KMONS:   C = rakshasa ; robe good_item . wand of cold | wand of fire | \
                      wand of lightning . \
                      glaive ego:freezing | glaive ego:flaming . \
                      potion of heal wounds | potion of might | \
                      potion of haste | nothing w:20
KMONS:   D = ettin ; great mace ego:flaming ident:type . \
                     great mace ego:freezing ident:type
KMONS:   E = deep elf annihilator ; robe randart | mundane ice dragon armour | \
                                  mundane fire dragon armour | robe w:1 . \
                                  scimitar ego:flaming | scimitar ego:freezing
KMONS:   F = tengu reaver
KMONS:   G = deep elf blademaster ; mundane crystal plate armour . \
                                  cutlass ego:flaming ident:type . \
                                  cutlass ego:freezing ident:type
KMONS:   H = ancient lich w:5 / lich
KMONS:   I = titan
KMONS:   J = golden dragon w:19 / golden dragon perm_ench:shapeshifter w:1 / \
             quicksilver dragon w:14 / \
             quicksilver dragon perm_ench:shapeshifter w:1
KMONS:   K = glowing shapeshifter hd:16 / glowing shapeshifter hd:15
KMONS:   L = crystal guardian
SHUFFLE: ABCD
NSUBST:  "~ = 1:{ / 1:[ / 1:( / 1:} / 1:] / 1:) / 1:<<. / 1:>>. / 3:T
NSUBST:  A = 1:A / 1:F / *:1, B = 1:B / 1:G / *:1
NSUBST:  C = 1:CC1 / 1:H / *:1, D = 1:D / 1:II1 / *:1
SHUFFLE: ABCDEF, GHIJKL, "X / ~Y / ^Z, +-=
SUBST:   "X = bX..., ~Y = bbY.., ^Z = b., v : v:3 b:2, c : c:3 b:2, d = c
SUBST:   + = +:14 .:1, - = +:4 .:1, = = +:2 .:3, * = *:9 |:1, % = %:14 *:5 .:1
SHUFFLE: XY
SUBST:   X : w:60 l., Y : l:40 w., l = lll., 1 : 1:99 .:1
KPROP:   l = no_cloud_gen
:  local Z = you.absdepth()
:  if you.absdepth() * 4 - 74 <= 0 then
:    subst("1 = 1:" .. math.max(72 - Z * 3, 3) ..
:             " 2:" .. math.max(52 - Z * 2, 3))
:  elseif you.absdepth() * 4 - 78 <= 0 then
:    subst("1 = 1:" .. math.max(72 - Z * 3, 3) ..
:             " 2:" .. math.max(52 - Z * 2, 3) ..
:             " 3:" .. Z * 4 - 74)
:  else
:    subst("1 = 1:" .. math.max(72 - Z * 3, 3) ..
:             " 2:" .. math.max(52 - Z * 2, 3) ..
:             " 3:" .. Z * 4 - 74 .. " 4:" .. Z * 4 - 78)
:  end
MARKER:  T = lua:props_marker { portal=1 }
: _G.set_random_mon_list("place:" .. you.where() .. " w:150 / " ..
:   "fire giant / frost giant / rakshasa w:5 / deep elf knight w:5 / " ..
:   "glowing shapeshifter w:8 / simulacrum w:5 / dancing weapon w:2 / " ..
:   "fire crab w:5 / boulder beetle / mana viper w:5 / very ugly thing band w:5")
TILE:    b = wall_emerald
TILE:    c = wall_crystal_squares
TILE:    v = dngn_metal_wall_darkgray
LFLOORTILE: floor_pebble_green
LFLOORCOL:  white
MAP
                                          bbb
                                         bbYbb
                                         b.%.b
                                         b.Y.b
          bbb                            b.B.b    bbb
         bbXbb                       bbbbc...cbbbbbYbb
         b.%.b                      bb...........b...b
         b.X.b                      bY.Y...Y...Y.-.Y.b
         b.A.b    bbb               bb...........b...b
     bbbbc...cbbbbbXbb               bb-bb...bbbbb...cbbbb
    bb...........b...b               b...b.1.b...........bb
    bX.X...X...X.-.X.b               b.Y.b.Y.b%Y1..Y..BY%Yb
    bb...........b...b    bbb        b...b.%.b...........bb
     bb-bb...bbbbb...bbbbbbXbb   bbbbb...bbbbvbbbb...cbbbb
     b...b.1.b....1......b...b  bb......1....b.%.b...b
     b.X.b.X.b%"...X...X.-.X.b  bY.Y...Y...~%b.~.b.Y.b
     b...b.%.b......1....b...b  bb....1......b...b...b
 bbbbc...bbbbvbbbb...bbbbb...bbbbbb-bb...bbbbb...bb-bb
bb...........b.%.b...b....1......b...b...b......1....bb
bX%XA..X..1X%b.".b.".b%"...X...X.+.Z.b.~.b%~...Y...Y.Yb
bb...........b...b.%.b......1....b...b.%.b....1......bb
 bbbbc...bbbbb...bbbbvbbbb...bbbbb...bbbbvbbbb...bbbbb
     b...b....1......b.%.b...b......1....b.%.b...b
     b.X.-.X...X..."%b.Z.b.".b.Z...Z...Z%b.~.b.Y.b
     b...b......1....b...b.%.b....B......b...b...b
     bbXbbbbbb...bbbbb...bbbbv+d+b...bbbbb...bb-bb
      bbb    b...b....1......b...b...b......1....bb
             b.X.-.Z...Z...Z%b.Z.b.Z.b%~...Y...Y.Yb
             b...b......A....b...b.%.b....1......bb
             bbXbbb+bb...bbbbb...bbbbvbbbb...bbbbb
              bbbb...b...+....A*B....+...b...b
                 b.Y.b.Z.d.Z..*Z*..Z.d.Z.b.Y.b
                 b...b...+....C*D....+...b...bbbb
             bbbbb...bbbbvbbbb...bbbbb...bb+bbbXbb
            bb......1....b.%.b...b....D......b...b
            bY.Y...Y...~%b.Z.b.Z.b%Z...Z...Z.=.X.b
            bb....1......b...b...b......1....b...b    bbb
             bb=bb...bbbbb...b+d+vbbbb...bbbbb...bbbbbbXbb
             b...b...b......C....b.%.b...b....1......b...b
             b.Y.b.~.b%Z...Z...Z.b.".b.Z.b%"...X...X.=.X.b
             b...b.%.b....1......b...b.%.b......1....b...b
         bbbbb...bbbbvbbbb...bbbbb...bbbbvbbbb...bbbbb...cbbbb
        bb......1....b.%.b...b....1......b.%.b...b...........bb
        bY.Y...Y...~%b.Y.b.Z.+.X...X..."%b.".b.".b%X1..X..DX%Xb
        bb....1......b...b...b......1....b...b.%.b...........bb
         bb=bb...bbbbb...bb=bbbbbb...bbbbb...bbbbvbbbb...cbbbb
         b...b...b......1....bb  b...b....1......b.%.b...b
         b.Y.b.~.b%~...Y...~.Yb  b.X.=.X...X..."%b.X.b.X.b
         b...b.%.b....1......bb  b...b......1....b.1.b...b
     bbbbc...bbbbvbbbb...bbbbb   bbXbbbbbb...bbbbb...bb=bb
    bb...........b.%.b...b        bbb    b...b...........bb
    bY%YC..Y..1Y%b.Y.b.Y.b               b.X.=.X...X...X.Xb
    bb...........b.1.b...b               b...b...........bb
     bbbbc...bbbbb...bb=bb               bbXbbbbbc...cbbbb
         b...b...........bb               bbb    b.D.b
         b.Y.=.Y...Y...Y.Yb                      b.X.b
         b...b...........bb                      b.%.b
         bbYbbbbbc...cbbbb                       bbXbb
          bbb    b.C.b                            bbb
                 b.Y.b
                 b.%.b
                 bbYbb
                  bbb
ENDMAP

################################################################################
# IT IS AN EMPTY, ROTTEN WORLD.
# LIE DOWN. FORGET YOUR TROUBLES.
# SMILE.
NAME:    hangedman_pleasuredromes
TAGS:    no_monster_gen no_item_gen uniq_d_encompass
ORIENT:  encompass
PLACE:   Depths:$
DEPTH:   Depths:2-
WEIGHT:  1 (Depths:$), 10
KMONS:   & = draconian knight / draconian monk w:15 / \
         draconian annihilator / deep elf blademaster w:15 / \
         deep elf master archer w:15 / lich w:15 / ancient lich w:15 / \
         balrug w:5 / executioner w:5
KMONS:   Z = base draconian
KFEAT:   z = known zot trap w:499 / floor w:1
KFEAT:   Z = enter_zot
SHUFFLE: Y], y}, {([>, HI / HI / IH, JK / JK / KJ
SHUFFLE: NO / NO / ON, PQ / PQ / QP, RS / RS / SR, lm / lm / ml
NSUBST:  ] = 1:) / 1:]
SUBST:   B : b.., C : cb., D : x.., E : xx., F : xx.
SUBST:   HJOQRm = x, IKNPSl = ., L : xx., M : xx., j : x.., k : x.., n : x.
SUBST:   b : b:6 v:1 c:1, ^ = 099888
SUBST:   y = Y, & = &:26 0:1
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
SUBST:  )]} = ., > = <, " = `
NSUBST: ' = 1:Z / 2:` / *:`
: end
SUBST:  '" = .
MARKER: Y = lua:props_marker { portal=1 }
FTILE:  Z` = floor_marble
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxJ.............xxxxxx
xxxxxxxxxxx......c.........xxx
xxxxxxxxx"".....cbc..x......xxx
xxxxxxxxx".....cbcbc.x......Mxxxx
xxxxxx'"0.....xF.b.cc...n....Lxxxx
xxxxxx""0....x...x..cc.x.......xxx
xxxxxx00.........b..0cc.........xxx
xxxx"".....x..xxb.bxb.cc..9.....xxx
xxJx".....x..E...b...b.cc0..n...xxx
xx.......x.......xx.0x.9cc.x.....xx
xx.............F...x.b*..cc......xx
xx.....x..E........xb.bxb.cc.xx..xx
xx....x..x...........b.0.b8cc....xxxxxxxxxxxxx
xx...cF..x..F....%...xx.9x..cc...xxxxxxxxxxxxxx
xx..cb...b......%*.....x.b...bc..xxxx0.%.%.0.xxx
xx.cbcbxb.bx...%*.*%...xb.bxbcbc.xxxxx........xxx
xx..cb...b.x.....*%....x.b...bc..xxxxxx........xxx
xx...cc..x..xx...%...xx..x..cc...xxxxxxx%.......xxx
xx....cc0b00.b.......b%89b9cc....xxxxxxxx...I.x.xxxx
xx..xx.cc.bcb.bx...xb.bxb.cc..xx.xx..xxxxx...H.^..xxx
xx......cc..*b.x...x%b%*|cc......xx....xxxx.x...x..xxx
xx.....x.cc9.x0.xxx.8x*&cc.......xx.......xx...H....xxx
xxxx..n..0cc.b09.b..9b|cc.....xMx.x.xLx.....x.x.I....xx
 xxx....9..cc.bxb.bxb.cc.9.xxx...xxx...xxx...x......0xx
  xx........cc8..b..9cc.........x...x........xx......xx
  xxxxL....x.cc..x..cc...x.0..SR..0..RS..0.x..xx....%xx
  xxxxxM..n...cc.b.cc....x....SR.....RS....x..xxx%...xx
     xxx.....x.cbcbc.....x....SR.....RS....x..xxxx..%xx
    xxcc.....x..cbc..x..x..SSSJ.x.Q.x.HSSS..x..xxxx..xx
    xcc..........c...x..L..RRR.K.cPc.I.RRR..M..xxxxx0xx
   xxc.xx...............x.x...x.J...H.x...x.x...xxxxxxx
   xc0.xx...xxxxxxxxxxxx.x.....c.>.[.c.....x....xxxxxxx
   xc....xx.....nnnnxxxxxx.0..NO..z..ON..0.xxxxxxxxxxxxxxx
  xc.....xx........nx....x.....c.(.{.c.....x.xxxxxxxxxxxxxx
  xc.......xx......nx...x.x...x.H...J.x...x.x............xxxxxx
  xxc......xx.......xx..M..RRR.I.cPc.K.RRR..L......c......xxxxx
   xc...j....xx......x..x..SSSH.x.Q.x.JSSS..x..Y..cbc........xx
   xxc.......xx.c....x...x....SR.....RS....x]....xbcbx.......xxx
    xc..........b....xx..x....SR.....RS....x....x..b..x......Lxxxxx
    x0cc......cb%bc...x..x.0..SR..0..RS..0.x.x.x..0c...x.x....Mxxxx
    xc..cc..cc.0b.....KJ........x...x.....y....b..0b90.b.........xx
    xc....cc....c.xx..KJ...xxx...xxx...xxx.}.xb.bcbcbcb.bx.......xxx
    xxc........c..xx...KJ.....xLx.x.xMx]....x..b..%b%..b..x......xxx
     xc.....k..c....xx..KJJ.......xx.....x.x..0c.0ccc0.c0..x.x....xxx
     xxc..%...c.....xx...KKxx.....xx.......b.00b*cc&cc%b89.b.......xx
     xxc..%%..c.......xx....xxxx..xx..Y..xb.bcbcbc8x8cbcbcb.bx.....xx
     xc..k.....c......xx.......xxxxx....x..b..*b8..x..8b*..b..x....xx
    xxc........c...j....xx......nnnx...x...c.0cc.0|x|*0cc0.c...x...xx
    xc....cc....c.......xx.c......nx..cb.00c%cc8.|x%*|.9cc*b99.bc..xx
    xc0.cc..cc."b".........b......nx.cbcbccccc&xxx%|%xxxccbcbcbcbc.xx
    x.cc......cb'bc......cb%bc....nx..cb..9c%cc8.|*%x.....%C...Bx..xx
    xc........0"b".cc..cc.0b.......x...x..0c.0cc.*|x....C..x...x...xx
   xxc.mm....ll0c....cc....c.xx....x....x..b..%c80.x.....%.x..x....xx
   xc..mm...%ll..c........c..xx....x.....xb.bccccc9x....D.x........xx
   xc......xx%...c.....k..c....xx..........b..8c*ccc.C%D...........xx
  xc%.....%xx.....c..%...c.....xx........x.x.9.c.0cc.....D.x.......xx
  xc%....xx%......c..%%..c.......xx.........x..b..*b%..x.........HOxx
  xxc...%xx......c..k.....c......xx..........xb.bcbcCxx..x.......Pxxx
   xc..ll%...mm..c........c...j....xx..........b..9b........"""JRlxxx
   xxc9ll....mm.c....cc....c.......xx.cxM....x.x..9c........"'"jxxxxx
    xc^9........c0.ccxxcc..c.........ccxxL......x..b..x....."""kxxxxx
    xxcc......cc.ccxxxxxxcc0cc.....0ccxxxx.......xbcBx......Jjklxxxxx
     xxxcc%%ccxxxxxx    xxxxxxcc..ccxxx xx........cbx.......Rxxxxxxxx
       xxxccxxx              xxxccxxx   xxxxx......c......HPlxxxxxxxx
         xxxx                  xxxx     xxxxxx............Oxxxxxxxxxx
                                            xxxxxxxxxxxxxxxxxxxxxxxxx
                                             xxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Maps from the now-defunct Radiant Caverns portal vault idea.
# Original design by rkd; modernised by Grunt.

NAME:   radiant_caverns_a_rkd
TAGS:   no_monster_gen no_item_gen uniq_d_encompass
ORIENT: encompass
: radiant_caverns_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx{.(.[.....xxxxxxxxxlllxxxsss..xxxxxxxxxxxxxxx.....H....xxxxxxxxxxxx
xx...........xxxxxxxlllxxxxssJJ..xxxxxxxxxxx..V..HH...H...xxxxxxxxxxx
xx}..........xxxxxlllxxxxx).JJ...xxxxxxx............xx....xxxxxxxxxxx
xxxxxxx........xxxlllxxxxxxx...xx..xxxxx..VV...xxllxxxx.V...xxxxxxxxx
xxxxxxxx....KK..xlllxxxxxxxxxxxxxx..xx...VV...xxllxxxx......xxxxxxxxx
xxxxxxxx....KK.Klllxxxxxxxxxxxxxxxx.........xxxllxxxx...4..xxxxxxxxxx
xxxxxxx.......Klll......xxxxxxxxxxxx......xxxxll......V.4.xxxxxxxxxxx
xxxxxx..V...xxlll....N.N.xxxxxxxxxx.....xxxxxll.VV.......xxxxxxxxxxxx
xxxxxx....xxxlllx....N.N..xxxxxxxxx..V.xxxxxll34..xxxxxxxxxxxxxxxxxxx
xxxxx....xxllllxx..........xxxxxxx.SS.xxxxxllx4....xxxxxxxxxxxxxxxxxx
xxxxx...xxllllxx.L..xx..L..xxxxxx.SS.xxxxxllxxx..V..xxxxxxxxxxxxxxxxx
xxxx.M..xlllxxx....xxxx..LV.xxxx.SS.xxxxxllxxxxx.3...xxxxxxxxxxxxxxxx
xxxx.M..lllxx.....xxxxxx.....xx....xxxxxllssxxxx.34..xxxxxxxxxxxxxxxx
xxxx..MM......L..xxxxxxxx....xx...xxxxxllssY<xx..V..xxxxxxxxxxxxxxxxx
xxxxlll...V.....xxxxxxxxxx.L.....xxxxxllxxxxxx.3...xxxxxxxxxxxxxxxxxx
xxxlllx.....L..xxxxxxxxxxxxxxxxxxxxxxllxxxxx1...xxxxxxxxxxxxxxxxxxxxx
xxlllxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxllxxxx...xxxxxxxxxxxxxxxxxxxxxxxx
xllxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx......V...xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxb.bxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxbb.bxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxbb...bxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxbbbbbbbbbbbbbbb..]..bbbbbbbbbbbbbbbxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxbb.................................bbxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxbb......................V............bbxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxbb.....5..666...V.........mmm.5......j.bbxxxxxxxxxxxxxx
xxxxxxxxxxxxxbb.h..V....666.......5.....mmm....V....j.bbxxxxxxxxxxxxx
xxxxxxxxxxxxbb.h..5......V..5..........5........V..j.j.bbxxxxxxxxxxxx
xxxxxxxxxxxbbww..h..n............V................n...wwbbxxxxxxxxxxx
xxxxxxxxxxbbwwwwh...V.......n..n.5.n...n.i.......5..jwwwwbbxxxxxxxxxx
xxxxxxxxxxbwwwwwwV..5....wwwwww.......wwwwwwii......wwwwwwbxxxxxxxxxx
xxxxxxxxxxbsswwwwwg.....wwwwwwww.n.n.wwwwwwwwi..V..wwwwwssbxxxxxxxxxx
xxxxxxxxxxbYsswwwwg.ggnwwbbbbbbww...wwbbbbbbww.n.n.wwwwssYbxxxxxxxxxx
xxxxxxxxxxbbsswwwwwwwwwwwbuu..bww.V.wwb.yu.bwwwwwwwwwwwssbbxxxxxxxxxx
xxxxxxxxxxxbbswwwwwwwwwwwb.yAubww.V.wwbuAuubwwwwwwwwwwwsbbxxxxxxxxxxx
xxxxxxxxxxxxbbwwwwbbbbbbwb.z.ubww...wwb.z..bwbbbbbbwwwwbbxxxxxxxxxxxx
xxxxxxxxxxxxxbbwwwbuuuzbwb+bbbbbb+++bbbbbb+bbb.u.ubwwwbbxxxxxxxxxxxxx
xxxxxxxxxxxxxxbbwwbuA..bbb..b...........b..bbbyzAubwwbbxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxbbwby...bb...b...........b...bb..u.bwbbxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxbbbbbb.b....b...b...b...b....b.bbbbbbxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxbbb+b..b+b.....7.....b+b..b+bbbxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbb....b.b...b7.7b...b.b....bbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbb....b.b...........b.b....bbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbb....bzbqq.b...b.qqbzb....bbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbbz1..b.bbq.......qbb.b..1zbbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbbbbbbb.Ybbbb+++bbbbY.bbbbbbbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbbuuuub..bb.......bb..buuuubbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbb.uuub.bb.........bb.buuu.bbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbb..uub.b...ttttt...b.buu..bbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxbb....b.....ttttt.....b....bbxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxbbbb+bbbb..ttttt..bbbbb+bbbxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxbb.zyz.b.........b.yzz.bbxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxbb....bb.bb>bb.bb....bbxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxbb.......bbb.......bbxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxbbbbbbbbbbbbbbbbbbbxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   radiant_caverns_b_rkd
TAGS:   no_monster_gen no_item_gen uniq_d_encompass
ORIENT: encompass
: radiant_caverns_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx.HH.....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxHHV..x..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx....xxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxx..3.33...1.......xxb.bxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx..xxxxxxxxxxxxxxxxxbb.bxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx.Vxxxxxxxxxxxxxxxxxbb...bxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx4.xxxxbbbbbbbbbbbbbbb..]..bbbbbbbbbbbbbbbxxxxxxxxxxxxxxxxx
xxxxxxxxxx..xxxxbb.................................bbxxxxxxxxxxxxxxxx
xxxxxxxxx.V.xxxbb......................V............bbxxxxxxxxxxxxxxx
xxxxxxxx.44xxxbb.....5..666...V.........mmm.5......j.bbxxxxxxxxxxxxxx
xxxxxxx..4xxxbb.h..V....666.......5.....mmm....V....j.bbxxxxxxxxxxxxx
xxxxxx.3.xxxbb.h..5......V..5..........5........V..j.j.bbxxxxxxxxxxxx
xxxxx.3.xxxbbww..h..n............V................n...wwbbxxxxxxxxxxx
xxxxx..xxxbbwwwwh...V.......n..n.5.n...n.i.......5..jwwwwbbxxxxxxxxxx
xxxxx.xxxxbwwwwwwV..5....wwwwww.......wwwwwwii......wwwwwwbxxxxxxxxxx
xxxxx.xxxxbsswwwwwg.....wwwwwwww.n.n.wwwwwwwwi..V..wwwwwssbxxxxxxxxxx
xxxxx.xxxxbYsswwwwg.ggnwwbbbbbbww...wwbbbbbbww.n.n.wwwwssYbxxxxxxxxxx
xxxx...xxxbbsswwwwwwwwwwwbuu..bww.V.wwb.yu.bwwwwwwwwwwwssbbxxxxxxxxxx
xxx.....xxxbbswwwwwwwwwwwb.yAubww.V.wwbuAuubwwwwwwwwwwwsbbxxxxxxxxxxx
xx..NN...xxxbbwwwwbbbbbbwb.z.ubww...wwb.z..bwbbbbbbwwwwbbxxxxxxxxxxxx
xx..NN.V.xxxxbbwwwbuuuzbwb+bbbbbb+++bbbbbb+bbb.u.ubwwwbbxxxxxxxxxxxxx
xx.V.......xxxbbwwbuA..bbb..b...........b..bbbyzAubwwbbxxxxxxxxxxxxxx
xx..JJV..xx.xxxbbwby...bb...b...........b...bb..u.bwbbxxxxxxxxxxxxxxx
xx.......xxx.xxxbbbbbb.b....b...b...b...b....b.bbbbbbxxxxxxxxxxxxxxxx
xxx.....xxxxx.xxxxxbbb+b..b+b.....7.....b+b..b+bbbxxxxxxxxxxxxxxxxxxx
xxxx...xxxxxxxV.xxxxbb....b.b...b7.7b...b.b....bbxxxxxxxxxxxxxxxxxxxx
xxxxx..xxxxxxxxV.xxxbb....b.b...........b.b....bbxlllllxxxxxxxxxxxxxx
xxxxxx..xxxxxxxx..xxbb....bzbqq.b...b.qqbzb....bbxlKK.lxxxxxxxxxxxxxx
xxxxxxx..xxxxxxx..xxbbz1..b.bbq.......qbb.b..1zbbxl.K.lxxxxxxxxxxxxxx
xxxxxxxx..xxxxxx..xxbbbbbbb.Ybbbb+++bbbbY.bbbbbbbxlllllxxxxxllxxxxxxx
xxxxxxxx..xxxxxx..xxbbuuuub..bb.......bb..buuuubbxxxllxxxxxllx.xxxxxx
xxxxxxxx..xxxxxx..xxbb.uuub.bb.........bb.buuu.bbxxllxxxxxllxx.xxxxxx
xxxxxxxx..xxxxxxJ.xxbb..uub.b...ttttt...b.buu..bbxllxxxxxllxx...xxxxx
xxxxxx.......xxx.Jxxbb....b.....ttttt.....b....bbxlllxxxxlxxss..<xxxx
xxxxxx.SSSVV.xxx..xxxbbbb+bbbb..ttttt..bbbbb+bbbxllxllxxlllxxsY.xxxxx
xxxxx..SSS.V.xx....xxxbb.zyz.b.........b.yzz.bbxllxxllxllxllxxxxxxxxx
xxxxx.V......xx.JJssxxxbb....bb.bb>bb.bb....bbxxlxx..lllxxxllxxxxxxxx
xxxxx........xx).sssxxxxbb.......bbb.......bbxxllxx..LLL..xxllxxxxxxx
xxxxxxxxxxx.xxxxxxxxxxxxxbbbbbbbbbbbbbbbbbbbxxx.llxx.LL..ssxxlxxxxxxx
xxxxxxxxxxx...xxxxxxMM..xxxxxxxxxxxxxxxxxxxxxxx.Vllxxssssxxxxlxxxxxxx
xxM...xxxxxxx.xxxxxxMM.V.xxxx............xxxxxxx.Kllxx<xxxllllxxxxxxx
xxs.x.xxxxxxx....xxx.......xx.xxx.[..xxx.xx.....V.Kllxxxlllxxxxxxxxxx
xxssx..M.....xxxx...xxxxxx....xx.{.(.}xx....xxxxxxxxlllllxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

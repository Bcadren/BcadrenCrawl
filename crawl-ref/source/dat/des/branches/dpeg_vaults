NAME:  vaults_dpeg_ring
TAGS:  vaults_empty
MAP
.xx.
x..x
x..x
.xx.
ENDMAP

NAME:  vaults_dpeg_emergency_exit
TAGS:  vaults_empty
DEPTH: V:1-3
KFEAT: ^ = known shaft trap
# no_tele_into not necessary, in my opinion (buy YMMV)
MAP
.....
.mmm.
.m^m.
.mmm.
.....
ENDMAP

NAME:  vaults_dpeg_hooks
TAGS:  vaults_empty
SUBST: _ : +..
MAP
.......
.xxx_x.
.x...x.
.x...x.
.x_xxx.
.......
ENDMAP

NAME:  vaults_dpeg_diagonal
TAGS:  vaults_empty
SUBST: _ : xxxx$ , $ : $SSS. , S = $$.
MAP
xxxx..x
xxx..xx
xx..xxx
x..___x
..x___x
.xx___x
xxxxxxx
ENDMAP

NAME:  vaults_dpeg_encased_lava
TAGS:  vaults_empty
KPROP: l = no_rtele_into
MAP
.......
.nnnnn.
.nllln.
.nllln.
.nnnnn.
.......
ENDMAP

NAME:  vaults_dpeg_spiral
TAGS:  vaults_empty
SUBST: $ : $$$(()
MAP
    xxxxxxxx
   xxxx...xxx
  xx........xx
 xx...xxxx...xx
xx...xxxxxx..xx
xx...x...xxx..x
xx...x.$.xxx..x
xx...x...xx..xx
xx...xx......xx
xx.@..xxx..xxx
       xxxxxx
ENDMAP

NAME:  vaults_dpeg_cross
TAGS:  vaults_empty
SUBST: _ : xxxxx...TG, T : TUt, t = TU
MAP
   x..x
  xx..xx
 xxx..xxx
xxx_.._xxx
..........
..........
xxx_.._xxx
 xxx..xxx
  xx..xx
   x..x
ENDMAP

NAME:    vaults_dpeg_doors
TAGS:    vaults_empty
SHUFFLE: _+
SUBST:   _ : ...+
MAP
xxxxxxxxxxxxxxxxxxxxx
..xxxxxxxxxxxxxxxxx..
...._..xxxxxxx..._...
...._.....+......_...
...._.....+......_...
...._..xxxxxxx..._...
..xxxxxxxxxxxxxxxxx..
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   dpeg_vaults_monster_box
TAGS:   vaults_room
SUBST:  - : +xx, _ : +xx
SUBST:  b : xxxxb0009.
NSUBST: 9 = 1:S / *:9
KFEAT:  S = armour shop / weapon shop
SUBST:  . = ....0
MAP
   @
xxx++xxx
x......x
x..bb..x
-..bb..-
x..bb..x
x......x
xxx__xxx
ENDMAP

NAME:   dpeg_vaults_monster_row
TAGS:   vaults_hard
# could perhaps do with no_rotate, for consolists
SUBST:  0 : 0000000z, z : z., 0 = 0099..
KMONS:  z = human skeleton
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
xx00xx00xx00xx00xx00xx00xx
xx..xx..xx..xx..xx..xx..xx
..........................
ENDMAP

NAME:   dpeg_vaults_monster_doublerow
TAGS:   vaults_room
# could perhaps do with no_rotate, for consolists
WEIGHT: 1
SUBST:  0 : 0000000z, z : z., 0 = 0009..
KMONS:  z = human skeleton
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
xx00xx00xx00xx00xx00xx00xx
xx..xx..xx..xx..xx..xx..xx
x........................x
x.xx..xx..xx..xx..xx..xx.x
..xx00xx00xx00xx00xx00xx..
xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   dpeg_vaults_hating_casters
TAGS:   vaults_hard
MONS:   eye of draining
SUBST:  % = %%%$* , * = **|
NSUBST: . = 6:0 / *:.
SUBST:  0 = 00009, 8 = 889
SUBST:  _ : x. , + : +==
NSUBST: = = 1:+ / *=+x
MAP
    @@@
xxxx+++xxxx
x%%.....%%x
x%..000..%x
x_..mmm.._x
x_..m1m.._x
x_..mmm.._x
x...***...x
x8.......8x
xxxxxxxxxxx
ENDMAP

NAME:   dpeg_vaults_pockets_of_resistance
TAGS:   vaults_hard
SUBST:  0 = 0000099998
MAP
  xxx
 xxxxx    xxx
xx000xx  xxxxx
xx000xx  x000xx
xx000..  x000xx
 xx.......000xx
     ......xxx
     .....
   xx000xx
   xx000xx
   xx000xx
    xxxxx
     xxx
ENDMAP

NAME:   dpeg_vaults_waves
TAGS:   vaults_hard
SUBST:  = = +x
SUBST:  ^ : .x , - : .xx
SUBST:  * = **% , | = ||*
SUBST:  8 = 89
MAP
xxxxxx
x8..|x
x-..^xxxxxxxxxxxxxx
x8...+*.9...x%....x
x-...=..9...+..000+
x8...+*.9...x%....x
x-..^xxxxxxxxxxxxxx
x8..|x
xxxxxx
ENDMAP
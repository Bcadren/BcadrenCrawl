##############################################################################
# forest.des: Maps for the Forest.
# - entry vaults
# - minivaults
# - branch end vaults
##############################################################################

##############################################################################
# Forest entry vaults
##############################################################################

##############################################################################
NAME:   dummy_forest_entry
WEIGHT: 50
TAGS:   forest_entry
KFEAT:  O = enter_forest
MAP
O
ENDMAP

##############################################################################
# A really basic entry.
NAME:   grunt_forest_entry_basic
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   plant / nothing
MAP
.........
..t...t..
.t11.11t.
..1t.t1..
....O....
..1t.t1..
.t11.11t.
..t...t..
.........
ENDMAP

##############################################################################
# A small clearing.
NAME:   grunt_forest_entry_clearing
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   spriggan
MONS:   faun / spriggan rider
MONS:   wolf, black bear, grizzly bear
SUBST:  t = t.
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
.tt...tt.
t..Q.Q..t
t.t.Q.t.t
.Q.P.P.Q.
..Q.O.Q..
.Q.P.P.Q.
t.t.Q.t.t
t..Q.Q..t
.tt...tt.
ENDMAP

##############################################################################
# A secluded grove.
NAME:   forest_entry_grove
TAGS:   forest_entry vaults_entry_forest vaults_orient_s
KFEAT:  O = enter_forest
MONS:   spriggan
MONS:   faun / spriggan rider
MONS:   wolf, black bear, grizzly bear
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
   xxxxxxxxx
  xxxttttxxxx
 xxxttttttttxx
xxtttt...tttxxx
xttt..Q.Q..ttxx
xttt.Q.R.Q.tttx
xtt.........ttx
xtt.R..O..R.ttx
xtt..R.R.R..ttx
xttt..R.R..tttx
xttt.......tttx
xxtttt...ttttxx
 xxxtt..ttxxxx
  xxxtt..ttxx
   xxtt..ttx
ENDMAP

##############################################################################
# Forest minivaults
##############################################################################
NAME: grunt_forest_small_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 apple / w:1 pear / w:1 orange / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
.........
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_pond
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
MAP
 tt...tt
t.......t
t..WWW..t
..WwwwW..
..WwwwW..
..WwwwW..
t..WWW..t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_treed
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: spriggan
MAP
 tt...tt
t.......t
t.1...1.t
...ttt...
...t*t...
...ttt...
t.1...1.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statue_fruit
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 apple / w:1 pear / w:1 orange / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
....G....
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statues
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: faun, faun / satyr w:3
NSUBST: 7 = 3:1 / 1:2
MAP
 tt...tt
t.......t
t.G.7.G.t
.........
..7.*.7..
.........
t.G.7.G.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 apple / w:1 pear / w:1 orange / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.................t
...................
...................
...................
t.................t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_guards
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
MONS: spriggan, spriggan rider, spriggan air mage / spriggan berserker
NSUBST: P = 4:1 / 3:2 / 1:3
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.......P.P.......t
.......P%.%P.......
.........G.........
.......P*.%P.......
t.......P.P.......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_statues
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
KMONS: * = faun / spriggan berserker
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 apple / w:1 pear / w:1 orange / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t.......G.......t
tt....G.....G....tt
t.................t
...................
.....G...*...G.....
...................
t.................t
tt....G.....G....tt
 t.......G.......t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_slash
TAGS:  transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 apple / w:1 pear / w:1 orange / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  tww...........t
 ttwww..........tt
 t..www..........t
tt...www.........tt
t.....www.........t
.......wwW.........
........WWW........
.........Www.......
t.........www.....t
tt.........www...tt
 t..........www..t
 tt..........wwwtt
  t...........wwt
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_hut
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
COLOUR: -*c = lightgrey
FTILE:  -* = floor_normal
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t......ccccc......t
.......c--*c.......
.......+-9*c.......
.......c--*c.......
t......ccccc......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

# These vaults are derived from spriggan_forest.
NAME: kb_forest_berserker_hut_1
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
MAP
   tttttt
ttttPP.Tt
tccccT..@
tc%1c...T
tc..+..Tt
tc.1c...T
tcccct.Tt
tttT..ttt
 tTPTTt
 tTTttt
 tttt
ENDMAP

NAME: kb_forest_berserker_hut_2
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
KFEAT: _ = altar_trog
MAP
tt@t
t..tttttttt
t....xxxxxt
tT...x..1xt
tT...+...xt
ttt.tx..1xt
  tt.x.1_xt
   ttxxxxxt
    ttttttt
ENDMAP

NAME: kb_forest_spriggan_hut
DEPTH: Forest
MONS:  spriggan
SUBST: T = t / .
MAP
tt@tttt
t.ttttt
t.cccct
t.+.1ct
tTc1%ct
ttcccct
ttttttt
ENDMAP

NAME:  kb_forest_spriggan_shepherd
DEPTH: Forest
SUBST: T = t / t / P / .
COLOUR: "MN = green
KMONS: P = plant
MONS:  yak, spriggan ; robe . quarterstaff
ITEM:  cheese q:3
MAP
ttttttt@t
txxxtt.tttttttt
txd+"""""""tt.@
txxx""""""""".tt
ttt""2"""""""TTt
ttT""11"11"TTTtt
tt""""1""""Tttt
tt".11""2TTtt
tt""1""""Ttt
ttt""""TTTt
  ttttttttt
ENDMAP

NAME:  kb_forest_druid_statue
DEPTH: Forest
MONS:  spriggan druid
MAP
tt@tt
t.1.t
t1G1t
t.1.t
ttttt
ENDMAP

# XXX: this one doesn't work very well outside the corner, or as a primary
# vault...
NAME:   kb_forest_skeletal_bat_cave
DEPTH:  Forest
ORIENT: southeast
# a vanilla spriggan looks iffy here after st_ unnecromancized it... axe?
MONS:   spriggan
MONS:   bat skeleton
MAP
                   tttttt
                ttttxxxxx
            t@tttxxxxxxxx
            tt..xxxxxxxxx
          tttt....xxxxxxx
        tttt......}xxxxxx
       tt.....xxxxxxxxxxx
ttt  ttt....xxxxxxxxxxxxx
@.tttt...xxxxxxxx2xxx2xxx
tt.......xxxxx..2xxxx22xx
 tttxxxxx......xx.2xx..xx
txxxxxxxxxx....xxx.1.xxxx
 txxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: nicolae_forest_garden
DEPTH: Forest
TAGS: no_monster_gen no_item_gen no_trap_gen
SUBST: P = P., ; = ;;.
NSUBST: A = 1:A / *:.
KITEM: f = apple / orange / apricot / nothing
KMONS: L = faun
KMONS: A = satyr
KMONS: C = dryad
KMONS: P = plant
KMONS: k = giant goldfish
KFEAT: ; = .
KFEAT: k = w
MAP
        ;@@@;
      ;;;;;;;;;
    tt;;;P.P.;;;tt
   tttttPPPPPPPtttt;
  ;ttAfPP.L....tAtt;;;
 ;PPttP..P.P.PP.ftP;;;
 ;P....PPP...PPP.PP.;@
;;PP..PPPwwwC.P..PPP;@
;;;;..L.wkwwww.L.ttP;@
;@;PP..Pwwwwkw...fttt;
 @;;P..PP.wwwPP.ttttt
  @;PP.PPP..PP..PPttt
  ;;;Ptt.P.PPPPPP;;;;
  ;;;ttfAttPP;.;;;;;
    ;tttttt;;;;;;@
     ;;tt;;;;;;;
        ;;;;;
ENDMAP

NAME:    nicolae_forest_circular_grove
DEPTH:   Forest
ORIENT:  float
TAGS:    transparent
SHUFFLE: Aa/Bb
SUBST:   Aa = t, BT = .t, b = @, 1 = 11.
KMONS:   1 = faun w:3 / satyr w:1 / centaur w:1
KMONS:   2 = spriggan druid / spriggan air mage
KMONS:   3 = spriggan rider w:15 / dryad w:3
MAP
      tBBbbbBBt
     ttT..1..Ttt
    aAT.T.T.T.TAa
   aA.T.1.T.1.T.Aa
  aA1..ttAAAtt..1Aa
 tA..TBBT...TBBT..At
ttTT.BB..T3T..BB.TTtt
BT..tB.1TT.TT1.Bt..TB
B.T1tT.TTT.TTT.Tt1T.B
b...A.TTT...TTT.A...b
b1TTA.3...2...3.ATT1b
b...A.TTT...TTT.A...b
B.T1tT.TTT.TTT.Tt1T.B
BT..tB.1TT.TT1.Bt..TB
ttTT.BB..T3T..BB.TTtt
 tA..TBBT...TBBT..At
  aA1..ttAAAtt..1Aa
   aA.T.1.T.1.T.Aa
    aAT.T.T.T.TAa
     ttT..1..Ttt
      tBBbbbBBt
ENDMAP

NAME:    nicolae_forest_elemental_fundamentals
DEPTH:   Forest
TAGS:    transparent
SHUFFLE: abc
SUBST:   a = W, b = .W, c = ., T = tw
NSUBST:  W = 1 / W, . = 2 / .
SUBST:   ' = .
KMONS:   1 = elemental wellspring
KMONS:   2 = spriggan air mage
KFEAT:   1 = W
MAP
   ...@@...
 .....bb.....
..aaabbbbccc..
.aaaabbbbcccc.
.aaaWWWWWWccc.
ttTTWWWWWWTTtt
 tttTTWWTTttt
   tttttttt
ENDMAP

NAME:  nicolae_forest_firefly_meadow
DEPTH: Forest
TAGS:  transparent
KMONS: . = giant firefly w:1 / nothing w:15
MAP
 .......
.........
.........
.........
.........
.........
.........
.........
 .......
ENDMAP

NAME:   nicolae_forest_game_of_thorns
DEPTH:  Forest
TAGS:   no_monster_gen transparent
NSUBST: h = 2:h / *:.
NSUBST: l = 3:l / *:W
SUBST:  T = t., W = WWWWw, q = W.
KMONS:  h = thorn hunter
KMONS:  l = thorn lotus
KFEAT:  l = W
MAP
      ttttt
    TtTTTtttT
   ....q.TTqTTTT
  @..hWT.WWWl.qT
 T.T..qWWqqWWTTT
TT..WWWqT.TW.q.T
t.TqWlq..T..W..tt
t.TTWWWq.Wqh.W.Tt
tqWW...WWqWWWq.Tt
tWqh.WWq..WlWq..t
tT..Wq.TT.qWWWWqt
tT..W..h.WWq..qWt
tTTTTWlWWTTTWlW.t
tT...WWWWTTTWWT.T
 T.q.h..qW..TT..
 TT.qt.qWq.h..@
  TT.tTTq....q
   TTtttTTTtTT
      ttttt
ENDMAP

NAME:  nicolae_forest_godswood
DEPTH: Forest
TAGS:  transparent extra decor
MAP
ttttttttttt
..ttttttt..
@...ttt...@
.tt..t..tt.
tttt...tttt
ttt..t..ttt
tt.......tt
ttt..w..ttt
ttt.www.ttt
ttt..w..ttt
tt...C...tt
ttt..t..ttt
tttt...tttt
 ttttttttt
  ttttttt
ENDMAP

NAME:    nicolae_forest_grove_arrangements
DEPTH:   Forest
TAGS:    allow_dup transparent decor extra luniq
SHUFFLE: abcdefghi/''''T''''/T'''''''T/T'''T'''T/T'T'''T'T/T'T'T'T'T/TTT'''TTT/ \
         'T'T'T'T'/T'T''''T'/'T'TTT'T'/'T'''''T'/TTTT'TTTT/T'TTTTT'T/TT'TTT'TT/ \
         '''TTT'''/T'TT'TTTT/TT'T'T'TT/TT'''''TT/TT''T''TT/TT''TT'T'/T''''T'T'
SUBST:   abcdefghiT = ttttt., ' = ., _ = ._
CLEAR:   _
MAP
__________________
_................_
_..aa...bb...cc.._
_.aaaa.bbbb.cccc._
_.aaaa.bbbb.cccc._
_..aa...bb...cc.._
_................_
_..dd...ee...ff.._
_.dddd.eeee.ffff._
_.dddd.eeee.ffff._
_..dd...ee...ff.._
_................_
_..gg...hh...ii.._
_.gggg.hhhh.iiii._
_.gggg.hhhh.iiii._
_..gg...hh...ii.._
_................_
__________________
ENDMAP

NAME:   nicolae_forest_minotaur_hut
DEPTH:  Forest
NSUBST: a = t / ., b = . / t, d = t / ., 1 = 1 / .
KMONS:  1 = minotaur
KITEM:  1 = any good_item
MAP
ttt.@.ttt
tccc.ccct
tc.a.b.ct
tc.cac.ct
tc.a.b.ct
tcdcdc.ct
tc1d1b1ct
tccccccct
ttttttttt
ENDMAP

NAME:   nicolae_forest_no_swimming
DEPTH:  Forest
TAGS:   transparent
NSUBST: b = 3:2 / *:b
SUBST:  d = Ww, b = W.
NSUBST: w = 1 / w
KMONS:  1 = elemental wellspring
KMONS:  2 = water nymph
KFEAT:  1 = w
KFEAT:  2 = W
MAP
    .........
  .............
 .....bbbbb.....
 ....bbWWWbb....
....bbWWWWWbb....
...bbWWWWWWWbb...
..bbWWWdddWWWbb..
..bWWWdwwwdWWWb..
..bWWWdwwwdWWWb..
..bWWWdwwwdWWWb..
..bbWWWdddWWWbb..
...bbWWWWWWWbb...
....bbWWWWWbb....
 ....bbWWWbb....
 .....bbbbb.....
  .............
    .........
ENDMAP

NAME:    nicolae_forest_oklob_treasure_island
DEPTH:   Forest
TAGS:    no_pool_fixup
SHUFFLE: Aa/Bb/Cc/Dd/Ee/Ff/Gg/Hh/Ii, %*$
NSUBST:  ' = 1:^ / 1:o / *:'
SUBST:   abcde = w, fghi = t, ABCDE = .wW, FGHI = .t, ' = ':100 $$$%%
KFEAT:   ^ = teleport trap
KMONS:   o = oklob plant
MAP
       .....
     .........
    ...........
   ....GGFFF....
  ....GggfffF....
 ....GggGFFffF....
 ...GggG'''FeeE...
...AaaA'''''EeeE...
...AaA'''''''EeE...
...AaA'''''''EeE...
...AaA'''''''DdD...
...BbbB'''''DddD...
 ...BbbB'''DddD...
 ....BbbCCCcdD....
  ....BbccccC....
   ....BCCCC....
    ...........
     .........
       .....
ENDMAP

NAME:   nicolae_forest_orc_logging_accident
DEPTH:  Forest
TAGS:   decor extra no_monster_gen no_trap_gen
SUBST:  F = 't, T = _t
NSUBST: p = 4:p / *:., ' = o / '
SUBST:  ' = '''B, . = ....b
KPROP:  Bbop = bloody
SUBST:  B = ', b = .
: dgn.delayed_decay(_G, 'o', 'orc corpse, hand axe race:orcish / ' ..
:    'broad axe race:orcish, leather armour race:orcish / ' ..
:    'pair of gloves race:orcish, mundane quarterstaff ')
: dgn.delayed_decay(_G, 'p', 'orc corpse, hand axe race:orcish / ' ..
:    'broad axe race:orcish, leather armour race:orcish / pair of gloves race:orcish ')
KITEM:  w = mundane quarterstaff
KITEM:  s = meat ration / bread ration
FTILE:  'o = floor_dirt
CLEAR:  _
MAP
      TTTTTTTTTT
     TTttttttttTT
    TTttttttttttTT
   TTtttF'FttttttTT
  TTttttF''FFtttttTT
 TTttttF'''''FtttttTT
 TttttttF'''FtttttttT
 Ttt.p...'''..p.ttttT
TTt..xxxx........tptT
Tttp.xwwx...xxmx...tT
Tt...xww+...xpsx...tT
Tt.p.xwwxp..+.sm.p.tT
Tt...xxxx...x.sx..ttT
Tt..p.......xxmx..tTT
Ttt.....p......p.ttT
TTtttt..........ttTT
 TTTTtttt@@ttttttTT
    TTTT     TTTTT
ENDMAP

NAME:    nicolae_forest_quartered_pool_little
DEPTH:   Forest
TAGS:    transparent extra decor allow_dup luniq_qpool
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm, Nn, Oo, Pp, Qq
SUBST:   abcdefghijklmnopq = ., ABCDEFGHIJKLMNOPQ = W, 7 = t
MAP
    777777
   7qolLOQ7
  7pnkiIKNP7
 7pmjhfFHJMP7
7qnjgedDEGJNQ7
7okhecbBCEHKO7
7lifdbaABDFIL7
7LIFDBAabdfil7
7OKHECBbcehko7
7QNJGEDdegjnq7
 7PMJHFfhjmp7
  7PNKIiknp7
   7QOLloq7
    777777
ENDMAP

NAME:    nicolae_forest_quartered_pool_medium
DEPTH:   Forest
TAGS:    transparent extra decor allow_dup luniq_qpool
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu
SUBST:   abcdefghijklmnopqrstu = ., ABCDEFGHIJKLMNOPQRSTU = W, 7 = t
MAP
      7777
    77spPS77
   7urolLORU7
  7tqnkiIKNQT7
 7uqmjhfFHJMQU7
 7rnjgedDEGJNR7
7sokhecbBCEHKOS7
7plifdbaABDFILP7
7PLIFDBAabdfilp7
7SOKHECBbcehkos7
 7RNJGEDdegjnr7
 7UQMJHFfhjmqu7
  7TQNKIiknqt7
   7UROLloru7
    77SPps77
      7777
ENDMAP

NAME:    nicolae_forest_quartered_pool_super
DEPTH:   Forest
TAGS:    transparent extra decor allow_dup luniq_qpool
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu, Vv, Ww, Xx, Yy, Zz
SHUFFLE: 12, 34, 56
SUBST:   abcdefghijklmnopqrstuvwxyz135 = ., ABCDEFGHIJKLMNOPQRSTUVWXYZ246 = W
SUBST:   7 = t
MAP
      777777
    771xtTX277
   75zwspPSWZ67
  73yvrolLORVY47
 75yuqnkiIKNQUY67
 7zvqmjhfFHJMQVZ7
71wrnjgedDEGJNRW27
7xsokhecbBCEHKOSX7
7tplifdbaABDFILPT7
7TPLIFDBAabdfilpt7
7XSOKHECBbcehkosx7
72WRNJGEDdegjnrw17
 7ZVQMJHFfhjmqvz7
 76YUQNKIiknquy57
  74YVROLlorvy37
   76ZWSPpswz57
    772XTtx177
      777777
ENDMAP

NAME:    nicolae_forest_small_spriggan_camp
DEPTH:   Forest
SHUFFLE: Ff/Gg/Hh/Ii
SUBST:   ghi = .
KMONS:   D = patrolling spriggan defender
KMONS:   SGHI = spriggan
KMONS:   F = spriggan air mage / spriggan druid
KITEM:   f = potion of porridge / bread ration / apple / banana
MAP
t@@tt  ttttttt
t..tttttttttttt
t..ttttttgggttt
t...ttt.t.G.tt
t....D..tt.ttt
tt.D....S...ttt
ttttt.......ttt
 tttt.S..S...tt
 ttt......tt.tt
ttttt.tt.tt..tt
tti...t...tH.tt
ttiI..t.F.thhtt
tti...tffftttt
ttttttttttttt
  tttttttttt
ENDMAP

NAME:   nicolae_forest_thorn_lake
DEPTH:  Forest
TAGS:   transparent
SUBST:  b = W.
NSUBST: W = 3:1 / *:W, . = 1:2 / *:.
KMONS:  1 = thorn lotus
KMONS:  2 = thorn hunter
KFEAT:  1 = W
MAP
  tt@@ttt
  tt..ttt  tttt
  ttt..tttttttt
  ttt..tttttt.@
   ttt..ttt...@
   ttt..t....tt
 tttttt....tttt
 tttt....tttttt
 tt....t..ttt
 @...ttt..ttt
 @.tttttt..ttt
 tttttttt..ttt
 tttt.......tt
tttt........ttt
ttt...bbb...ttt
tt...bbWbb...tt
tt..bbWWWbb..tt
tt..bWWWWWb..tt
tt..bbWWWbb..tt
tt...bbWbb...tt
ttt...bbb...ttt
tttt.......tttt
 tttt.....tttt
  ttttttttttt
   ttttttttt
ENDMAP

NAME:  nicolae_forest_triple_paths
DEPTH: Forest
TAGS:  transparent extra decor
SUBST: T = t_
CLEAR: _
MAP
         Tt@tT
         Tt.tT
     @TTTTt.tTTTT@
    T..tttt.tttt..T
    TT....t.t....TT
     Tttt.t.t.tttT
    TTttt.t.t.tttTT
TTTTTtttt.t.t.ttttTTTTT
ttttttttt.t.t.ttttttttt
TTTTTtttt.t.t.ttttTTTTT
    TTttt.t.t.tttTT
     Tttt.t.t.tttT
    TT....t.t....TT
    T..tttt.tttt..T
     @TTTtt.ttTTT@
        TTt.tTT
         Tt@tT
ENDMAP

# Place the Enchantress on Forest:$ if the ending doesn't.
# XXX: this is only here because of how unique placement vaults are
# handled normally; this placement behaves considerably differently.
NAME:   uniq_enchantress
DEPTH:  Forest:$
CHANCE: 100%
MONS:   the Enchantress band
veto {{
  return you.uniques("the Enchantress")
}}
MAP
1
ENDMAP

##############################################################################
# Forest ends
##############################################################################
NAME:   grunt_forest_end_chambers
ORIENT: north
PLACE:  Forest:$
KITEM:  O = superb_item
KITEM:  1 = superb_item
KMONS:  O = the Enchantress
KMONS:  1 = spriggan defender
MONS:   spriggan defender, spriggan berserker / spriggan
MONS:   spriggan druid / spriggan, spriggan rider / spriggan
MONS:   spriggan air mage / spriggan, spriggan / nothing w:30
MONS:   faun / satyr w:5
TILE:   c = wall_marble
COLOUR: .|*%OT123456+ = lightgray
FTILE:  .|*%OT123456+ = floor_normal
MAP
tttttttttttttttttttttttttttttttttt
tttttttttttttccccccccttttttttttttt
tttttttttttccc||||||cccttttttttttt
ttttttttttcc..........cctttttttttt
tttttttttcc....4444....ccttttttttt
tttttttttc..............cttttttttt
ttttttttcc..6.5....5.6..cctttttttt
ttttttttc%.6..........6.%ctttttttt
ttttttttc*.6..........6.*ctttttttt
ttttttttc*.6....O1....6.*ctttttttt
ttttttttc*.6....11....6.*ctttttttt
ttttttttc*.6..........6.*ctttttttt
ttttttttc%.6..5....5..6.%ctttttttt
ttttttttcc..6........6..cctttttttt
tttttttttc..............cttttttttt
tttttttttcc....4224....ccttttttttt
ttttcccccccc..........cccccccctttt
tttcc......ccc......ccc......ccttt
ttcc.4.4.4..cccc++cccc..4.4.4.cctt
tcc..........c.6..6.c..........cct
tc%..........c+.66.+c..........%ct
tc%......1....cccccc....1......%ct
tc%....T.2....cccccc....2.T....%ct
tc%...........cccccc...........%ct
tc%.6........c+.66.+c........6.%ct
tcc.6........c.6..6.c........6.cct
ttc..66.....cccc++cccc.....66..ctt
ttccc..66..cccc....cccc..66..ccctt
ttttccc..cccc........cccc..cccLttt
tLttLLcccccc...6446...ccccccLtLtLt
ttLLLLtLLLcc..........cctLLLtttLLt
ttLLLLLLLLc..6......6..cLLtLLtttLt
tLLtLLL7LLc..6..TT..6..cLLLLtLLLLt
ttLLLLLLLLc..6..TT..6..cLLLLLLLLLt
tttLtLLLLLc..6......6..cLLtLLLtLLt
ttLLLLLtLLcc..........ccLLL7LLLLLt
ttLL7LLLLLLc...6666...cLLLLLtLL7Lt
tLLLLLLLLLLcc........ccLLtLLLLLLtt
ttLLtLL7LLLLccc....cccLL7LLLtLtttt
tttttLLLLLLLLLc++++cLLLLLLtttttttt
tttttttLLLLLLLLLLLLLLLLttLLLtttttt
tttttttttttLLLLLLLLLLtttLLLLLttttt
tttttttttttttttLLLLtttLLLttttttttt
ttttttttttttttttLLLLLLLttttttttttt
ttttttttttttttttL@LLtttttttttttttt
ENDMAP

NAME:   kb_forest_enchantress_palace
ORIENT: northeast
PLACE:  Forest:$
MONS:   spriggan
MONS:   spriggan rider
MONS:   spriggan druid
MONS:   spriggan berserker
MONS:   spriggan air mage
MONS:   spriggan defender
MONS:   the Enchantress
SUBST:  . = .:100 / 1:2 / 2:1 / 3:1 / 4:1 / 5:1
SUBST:  T = t / t / P / .
KMONS:  P = plant / plant / bush / nothing
KMONS:  R = plant
KMONS:  Q = bush
KMONS:  K = giant firefly
KITEM:  f = dagger / short sword / sabre / flail / whip / blowgun / bow
KITEM:  g = robe / cloak / cap / buckler / steam dragon armour w:1
KMONS:  L = spriggan ; robe . dagger
KITEM:  h = bread ration q:2
COLOUR: C = white
KFEAT:  C = stone_wall
TILE:   C = wall_marble
MAP
ttttttttttttttttttttttttttttttttttttttttttt
ttttttttttttttttttttttttttttttttttttttttttt
ttttttttttttttttttttttttttTTTTTttTTTTtttttt
tttttttttTtTTtttttttQTtttTT.T.Ttt.T.TTTtttt
ttttttttT*....QQPtt..ccccc....cccc.TT.Ttttt
tttCCCCCCCCCCCCCC....c..1c....c.1c.t.TTtttt
tttC.6.C6..6C$*|CtT..c.1.+....+..cT.TT.tttt
tttC7..+....+$*|Ctt..ccccc....cccc.TT.Ttttt
tttC.6.C6..6C$*|CttQ..................ttttt
tttCCCCCC++CCCCCCttttccccc....cccc......ttt
tttCgggC....CfffCttttchxLc....c1.c.....tttt
tttCggg+..1.+fffCttttch..+....+.5c..1...ttt
tttCgggC1...CfffCttttccccc....cccc...1.tttt
tttCCCCCC++CCCCCCttttt...................tt
tttCR.....1....RCccccccccc....Y.........ttt
tttC..1........1CcKxKxKxKc......2......tttt
tttCR1..5..5..1RCc.22.1..c.....22......tttt
tttCCCCCC++CCCCCCc+ccccccc....2.2.....ttttt
ttt..................................tttttt
@...................................ttttttt
t..TTttttt............Y..........ttt.tttttt
tttttttttttttttt...............tttttt...ttt
ttttttttttttttttttt.........tttttttttttt<tt
ttttttttttttttttttttttt....tttttttttttttttt
tttttttttttttttttttttt@tt..@ttttttttttttttt
ENDMAP

#NAME:   grunt_forest_end_winery
#PLACE:  Forest:$
#ORIENT: northeast
#MONS:   Pan band, satyr, faun, plant, faun
#KFEAT:  T = tree / floor
#SUBST:  X = xt
#CLEAR:  x
#KFEAT:  : = floor
#ITEM:   grape q:1 / grape q:2 / nothing w:180
#ITEM:   any potion / grape q:1 / nothing w:30
#TILE:   c = wall_vines
#FTILE:  e123+|%*$- = floor_vines
#veto {{
#  return you.uniques("Pan")
#}}
#MAP
#tttttttttttttttttttttttttttttttttttttttttttt
#Xttttttttttttttttttttttttttttttttttttttttttt
#XttTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTtt
#XttT.5...................................Ttt
#XttT..4d4d4.cccccccccccccccccccccccccccc.Ttt
#XttT..4d4d4.ce-e-e-e-e-e-e-eceeeeee+|||c.Ttt
#XttT..4d4d4.c-e-e-e-c-e-e-e-c------c|||c.Ttt
#XttT..4d4d4.ce-e2e-ece-e2e-ec------cc+cc.Ttt
#XttT..4d4d4.cc+ccccccc+cccccc------c%**c.Ttt
#XttT..4d4d45c-----3---3-----+----1-c%**c.Ttt
#XttT..4d4d4.c-------2-------+------c%**c.Ttt
#XtT...4d4d4.c--wwwwwwwwwww--+------c%**c.Ttt
#XtT...4d4d4.c--wwwwwwwwwww--c------c$$$c.Ttt
#XtT...4d4d4.c-----3---3-----c------c$$$c.Ttt
#XtT...4d4d4.c---------------ceeeeee+$$$c.Ttt
#XTT...4d4d4.cc++cccccccccccccccccccccccc.Ttt
#T.......5......5............5............Ttt
#@TT...44444444..444444444444..4444444444.Ttt
#TtT...dddddddd..dddddddddddd..dddddddddd.Ttt
#XtT...444444445.444444444444.54444444444.Ttt
#XtT...dddddddd..dddddddddddd..dddddddddd.Ttt
#XttT..44444444.54444444444445.4444444444.Ttt
#XttT..dddddddd..dddddddddddd..dddddddddd.Ttt
#XttT..44444444..444444444444..4444444444.Ttt
#XttT.....................................Ttt
#XttT.....................................Ttt
#XttTTTTTT...........TTTT...........TTTTTTTtt
#XttttttttTTTTT.TTTTTTTTTTTTTT.TTTTTTTTTTTTtt
#XXtttttttttttT.TttttttttttttT.Tttttttttttttt
#XXXXXXXXXXXXT@TXXXXXXXXXXXXXXT@TXXXXXXXXXXXt
#ENDMAP

#NAME:   grunt_forest_end_shrine
#PLACE:  Forest:$
#ORIENT: float
#MONS:   Sojobo band, tengu reaver, tengu warrior band / tengu conjurer band
#KFEAT:  _ = altar_vehumet
#KFEAT:  - = floor
#SUBST:  T = tx
#CLEAR:  x
#FTILE:  1|*$-+ = floor_dirt
#TILE:   c = wall_church
#veto {{
#  return you.uniques("Sojobo")
#}}
#MAP
#  TTTTTTTTTTTTT
# TTtttttttttttTT
#TTtt.........ttTT
#Ttt3.........3ttT
#Tt..ccccccccc..tTTTTTTTT
#Tt..c||c$$$$c..ttttttttt
#Tt..c|-c****c.2t..3....t
#Tt..cc+c----+....G.G.G..
#Tt2.c----1_-+..2.......@
#Tt..cc+c----+....G.G.G..
#Tt..c|-c****c.2t..3....t
#Tt..c||c$$$$c..ttttttttt
#Tt..ccccccccc..tTTTTTTTT
#Ttt3.........3ttT
#TTtt.........ttTT
# TTtttttttttttTT
#  TTTTTTTTTTTTT
#ENDMAP
